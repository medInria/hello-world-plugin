macro(PluginSetDependencies)
    foreach (package ${ARGN})
        string(TOUPPER ${package} UPPER_CASE_NAME)
        option(USE_SYSTEM_${UPPER_CASE_NAME} OFF)
        if (NOT USE_SYSTEM_${UPPER_CASE_NAME})
            if (NOT MEDINRIA_BUILD_DIR)
                set(MEDINRIA_BUILD_DIR MEDINRIA_BUILD_DIR-NOTFOUND CACHE STRING "The location of the medInria super-project build directory")
                message(FATAL_ERROR "You must set the MEDINRIA_BUILD_DIR variable")
            endif()
            set(EXTRA_FIND_PACKAGE_ARGS PATHS ${MEDINRIA_BUILD_DIR} PATH_SUFFIXES build install)
        endif()
        find_package(${package} ${${package}_REQUIRED_VERSION} REQUIRED ${EXTRA_FIND_PACKAGE_ARGS})
        if (${UPPER_CASE_NAME}_FOUND OR ${package}_FOUND)
            if (NOT ${package}_USE_FILE_NAME)
                if (EXISTS ${${UPPER_CASE_NAME}_USE_FILE})
                    include(${${UPPER_CASE_NAME}_USE_FILE})
                elseif (EXISTS ${${package}_USE_FILE})
                    include(${${package}_USE_FILE})
                else()
                    message(FATAL_ERROR "No use file for package ${package}")
                endif()
            else()
                include(${${${package}_USE_FILE_NAME}})
            endif()
        else()
            message(WARNING "Cannot find the package ${package}")   
        endif()
    endforeach()
endmacro()

set(Qt4_REQUIRED_VERSION 4.6)
set(Qt4_USE_FILE_NAME QT_USE_FILE)
PluginSetDependencies(Qt4 dtk medInria)
